/*

Copyright 2020 - tomw

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.

-------------------------------------------

Change history:

0.9.20 - tomw - Added Blind Tilt support.
0.9.19 - tomw - Improved/completed support for invertPosition.
0.9.16 - tomw - Add invertPosition option and simplify refresh() logic.
0.9.8  - tomw - Bugfix for windowShade and switch parameters.
0.9.5  - tomw - Swallow unknown API replies instead of using "unknown".
0.9.4  - tomw - Add SwitchLevel (dimmer) functionality for ease-of-use.  setLevel functions as an alias for setPosition.
0.9.2  - tomw - More Curtain debugging.
0.9.1  - tomw - Updated Curtain driver after debugging.  Thanks @dadarkgtprince for testing and feedback.
0.9.0  - tomw - Initial release.

*/

metadata
{
    definition(name: "SwitchBot Curtain", namespace: "tomw", author: "tomw", importUrl: "")
    {
        capability "Initialize"
        capability "Refresh"
        capability "RelaySwitch"
        capability "Switch"
        capability "SwitchLevel"
        capability "WindowShade"
    }
}

preferences
{
    section
    {
        input name: "invertPosition", type: "bool", title: "Invert position and level?", description: "(use this if your Hubitat attributes are backwards from the SwitchBot app)", defaultValue: false
    }
}

def initialize()
{
    sendEvent(name: "position", value: "unknown")
    sendEvent(name: "level", value: "unknown")
    sendEvent(name: "switch", value: "unknown")
    sendEvent(name: "windowShade", value: "unknown")
    
    refresh()
}

def refresh()
{
    unschedule(refresh)
    
    try
    {
        def id = getParent()?.getId(device.getDeviceNetworkId())
        def respData = getParent()?.readDeviceStatus(id)
        
        if(!respData)
        {
            return
        }
        
        if(null != respData.body?.slidePosition)
        {
            def pos = correctedPositions(respData.body.slidePosition)
            
            sendEvent(name: "position", value: pos?.position)
            sendEvent(name: "level", value: pos?.level)
            
            if("unknown" != pos?.windowShade)
            {
                // only send valid new value
                sendEvent(name: "windowShade", value: pos?.windowShade)
            }
            
            if("unknown" != pos?.switchState)
            {
                // only send valid new value
                sendEvent(name: "switch", value: pos?.switchState)
            }
        }
    }
    catch (Exception e)
    {
        log.debug "refresh() failed: ${e.message}"
    }
}

def on()
{
    open()
}

def off()
{
    close()
}

def open()
{
    def id = getParent()?.getId(device.getDeviceNetworkId())
    if(id)
    {
        def respData = getParent()?.writeDeviceCommand(id, "turnOn", "default", "command")
        runIn(30, refresh)
    }
}

def close()
{
    def id = getParent()?.getId(device.getDeviceNetworkId())
    if(id)
    {
        def respData = getParent()?.writeDeviceCommand(id, "turnOff", "default", "command")
        runIn(30, refresh)
    }
}

def setPosition(position)
{
    def id = getParent()?.getId(device.getDeviceNetworkId())
    if(id)
    {
        def pos = correctedPositions(position)
        def respData = getParent()?.writeDeviceCommand(id, "setPosition", "0,ff,${pos?.position}", "command")
        runIn(30, refresh)
    }    
}

def setLevel(level, duration = null)
{
    setPosition(level)    
}

def startPositionChange(direction)
{
    log.warn "startPositionChange not supported"
}

def stopPositionChange()
{
    log.warn "stopPositionChange not supported"
}

def doInvert()
{
    (null == invertPosition) ? false : invertPosition    
}

def correctedPositions(rawPos)
{
    if(null == rawPos) { return }
    
    rawPos = rawPos?.toInteger()
    
    def doInvert = doInvert()
    
    def windowShade = "unknown"
    def switchState = "unknown"
    
    switch(rawPos)
    {
        case 0..5:
            windowShade = !doInvert ? "closed" : "open"
            switchState = !doInvert ? "off" : "on"
            break
        
        case 95..100:
            windowShade = !doInvert ? "open" : "closed"
            switchState = !doInvert ? "on" : "off"
            break
        
        default:
            windowShade = "partially open"
            switchState = "on"
    }
    
    def position = doInvert ? 100 - rawPos : rawPos
    def level = position
    
    def retVal =
        [
            windowShade: windowShade,
            switchState: switchState,
            position: position,
            level: level
        ]
    
    return retVal
}
